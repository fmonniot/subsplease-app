plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {

    defaultConfig {
        // minSdkVersion <= targetSdkVersion <= compileSdkVersion
        minSdkVersion 30
        targetSdkVersion 32
        compileSdkVersion 32

        applicationId "eu.monniot.subpleaseapp"
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        buildConfigField "String", "BUILD_TIME", "\"${System.currentTimeMillis()}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        //useIR = true
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion composeVersion
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    // Force usage of the maven central version, won't be required once compose:ui-tooling uses it
    implementation 'org.jetbrains.kotlinx:kotlinx-collections-immutable-jvm:0.3.5'

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.ui:ui-util:$composeVersion"
    implementation "androidx.compose.material:material:$composeVersion"
    debugImplementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation 'androidx.navigation:navigation-compose:2.5.0-rc02'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.activity:activity-compose:1.6.0-alpha05'
    implementation "androidx.security:security-crypto:1.1.0-alpha03"
    implementation 'androidx.room:room-runtime:2.4.2'
    implementation 'androidx.room:room-ktx:2.4.2'

    kapt 'androidx.room:room-compiler:2.4.2'

    debugImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    implementation("io.coil-kt:coil-compose:2.1.0")
    implementation 'com.github.moove-it:fakeit:v0.7'

    implementation 'org.jsoup:jsoup:1.15.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.moshi:moshi:1.13.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.13.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    implementation 'ru.gildor.coroutines:kotlin-coroutines-okhttp:1.0'

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.3'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.room:room-testing:2.4.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}